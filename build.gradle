plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.9.2'
}

scmVersion {
    tag {
        prefix = 'json-avro-converter'
    }
    versionCreator 'versionWithBranch'

    hooks {
        pre 'fileUpdate', [files: ['README.md'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "'$v'"}]
        pre 'commit'
    }
}

sourceCompatibility = 1.8

allprojects {
    project.group = 'tech.allegro.schema.json2avro'
    project.version = scmVersion.version
}

subprojects {

    project.ext.versions = [
        spock: '1.1-groovy-2.4',
        groovy: '2.4.15'
    ]

    repositories {
        mavenCentral()
    }

    test {
        testLogging {
            exceptionFormat 'full'
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
    }
}
