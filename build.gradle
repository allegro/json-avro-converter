buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0'
    }
}

plugins {
    id 'java'
    id 'pl.allegro.tech.build.axion-release' version '1.9.0'
}

apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = 'json-avro-converter'
    }
    versionCreator 'versionWithBranch'

    hooks {
        pre 'fileUpdate', [files: ['README.md'], pattern: {v, p -> /'$v'/ }, replacement: {v, p -> "'$v'"}]
        pre 'commit'
    }
}

nexusStaging {
    packageGroup = "tech.allegro"

    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}

sourceCompatibility = 1.8

allprojects {
    project.group = 'tech.allegro.schema.json2avro'
    project.version = scmVersion.version
}

subprojects {

    project.ext.versions = [
        spock: '1.1-groovy-2.4'
    ]

    repositories {
        mavenCentral()
    }

    test {
        testLogging {
            exceptionFormat 'full'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}
