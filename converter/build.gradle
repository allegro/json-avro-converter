buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.davidmc24.gradle.plugin:gradle-avro-plugin:1.9.1'
    }
}

plugins {
    id 'java'
    id 'groovy'
    // does not work with Java 10
    // id 'jacoco'
    id 'me.champeau.gradle.jmh' version '0.4.5'
}

apply plugin: 'com.github.davidmc24.gradle.plugin.avro'
apply plugin: 'idea'

configurations {
    jmh
}

jmh {
    include = 'tech\\.allegro\\.schema\\.json2avro\\.converter\\..*'
    humanOutputFile = null
    jmhVersion = '1.20'
    iterations = 2
    timeOnIteration = '10s'
    fork = 1
    warmupIterations = 1
    warmup = '10s'
    failOnError = true
    threads = 1
    duplicateClassesStrategy = 'warn'
}

dependencies {
    compile group: 'org.apache.avro', name: 'avro', version: '1.9.2'

    testCompile group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: versions.groovy

    jmh 'org.openjdk.jmh:jmh-core:1.20'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.20'
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.jmh ]
    }
}

// Workaround for duplicated `BenchmarkList` and `CompilerHints` files from META-INF directory in jmh jar.
// Those duplications can prevent from running benchmark tests.
// More info https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}
