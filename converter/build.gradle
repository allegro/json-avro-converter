buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.me.champeau.gradle:jmh-gradle-plugin:0.3.0'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'maven'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'me.champeau.gradle.jmh' version '0.3.0'
}

apply plugin: 'idea'

configurations {
    jmh
}

jmh {
    include = 'tech\\.allegro\\.schema\\.json2avro\\.converter\\..*'
    humanOutputFile = null
    jmhVersion = '1.12'
    iterations = 2
    timeOnIteration = '10s'
    fork = 1
    warmupIterations = 1
    warmup = '10s'
    failOnError = true
    threads = 1
}

dependencies {
    compile group: 'org.apache.avro', name: 'avro', version: '1.8.1'

    testCompile group: 'org.spockframework', name: 'spock-core', version: versions.spock

    jmh 'org.openjdk.jmh:jmh-core:1.12'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
}

modifyPom {
    project {
        name project.name
        description 'JSON to Avro conversion tool designed to make migration to Avro easier'
        url 'https://github.com/allegro/json-avro-converter'
        inceptionYear '2016'

        scm {
            url 'https://github.com/allegro/json-avro-converter'
            connection 'scm:git@github.com:allegro/json-avro-converter.git'
            developerConnection 'scm:git@github.com:allegro/json-avro-converter.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'skylab'
                name 'Team Skylab'
            }
        }
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [ configurations.jmh ]
    }
}

// Workaround for duplicated `BenchmarkList` and `CompilerHints` files from META-INF directory in jmh jar.
// Those duplications can prevent from running benchmark tests.
// More info https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}
